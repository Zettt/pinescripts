// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Zettt
// 2021

//@version=6
indicator('Peak Reversal v3', shorttitle = 'Peak Reversal', overlay = true)

//
// INPUTS
//
// Keltner Channel Settings
group_keltner = "Keltner Channel Settings"
maType = input.string(title = 'MA Type', defval = 'EMA', options = ['EMA', 'SMA', 'RMA', 'VMA', 'HMA'], tooltip = 'The type of moving average to use for the Keltner Channel basis.', group = group_keltner)
keltnerEMAlength = input.int(title = 'MA Length', defval = 20, minval = 1, tooltip = 'The length of the MA used for the Keltner Channels.', group = group_keltner)
atrLength = input.int(title = 'ATR Length', defval = 14, minval = 1, tooltip = 'The length of the ATR used for the Keltner Channels.', group = group_keltner)
innerBandMultiplier = input.float(title = 'Inner Band Multiplier', defval = 2, minval = 0.1, step = 0.25, group = group_keltner)
outerBandMultiplier = input.float(title = 'Outer Band Multiplier', defval = 3, minval = 0.1, step = 0.25, group = group_keltner)

// Display Settings
group_display = "Display Settings"
showMeanEMA = input.bool(title = 'Show Mean EMA?', defval = true, tooltip = 'Will plot the Channel EMA (mean) in the middle of the bands.', group = group_display)
showInnerBand = input.bool(title = 'Show Inner Band?', defval = true, group = group_display)
showOuterBand = input.bool(title = 'Show Outer Band?', defval = false, group = group_display)
showDeviations = input.bool(title = 'Show Mean Deviations?', defval = true, tooltip = 'Indicates when candles start going outside the bands. The more candles outside the bands, the likelier a return to the mean becomes.', group = group_display)
showBandCross = input.bool(title = 'Show Band Crosses?', defval = true, tooltip = 'Show when candles cross bands.', group = group_display)
showFreeBars = input.bool(title = 'Show Free Bars?', defval = false, tooltip = 'Highlights bars where the entire candle is outside of the selected band, which can indicate extreme momentum.', group = group_display)

// Signal Settings
group_signals = "Signal Settings"
showOnlyFirstSignal = input.bool(title = 'Show Only First Signal', defval = true, tooltip = 'If checked, only the first signal after a reset will be shown.', group = group_signals)
signalBand = input.string(title = 'Signal Band', defval = 'Inner', options = ['Inner', 'Outer'], tooltip = 'Which of the bands will be used for all signal calculations.', group = group_signals)
signalSource = input.string(title = 'Signal Source', defval = 'Wick', options = ['Wick', 'Close'], tooltip = 'Choose the source for cross signals: "Wick" (high/low) or "Close". Free Bars will not be affected.', group = group_signals)

// Momentum Coloring
group_coloring = "Momentum Coloring"
maxMomentumBars = input.int(title = 'Max Momentum Bars', defval = 5, minval = 1, tooltip = 'The number of bars at which momentum coloring reaches its peak intensity. After this many bars, the color will stay constant.', group = group_coloring)
upMomentumColor = input.color(title = 'Up Momentum Color', defval = color.new(#b71c1c, 0), tooltip = 'Base color for upward momentum signals.', group = group_coloring)
downMomentumColor = input.color(title = 'Down Momentum Color', defval = color.new(#1b5e20, 0), tooltip = 'Base color for downward momentum signals.', group = group_coloring)

// Calculate Keltner Bands
float basis = switch maType
    'EMA' => ta.ema(close, keltnerEMAlength)
    'SMA' => ta.sma(close, keltnerEMAlength)
    'RMA' => ta.rma(close, keltnerEMAlength)
    'VMA' => ta.vwma(close, keltnerEMAlength)
    'HMA' => ta.hma(close, keltnerEMAlength)
    => na

atrRange = ta.atr(atrLength)
upInnerKeltnerBand = basis + atrRange * innerBandMultiplier
downInnerKeltnerBand = basis - atrRange * innerBandMultiplier
upOuterKeltnerBand = basis + atrRange * outerBandMultiplier
downOuterKeltnerBand = basis - atrRange * outerBandMultiplier


// Plot Bands
plot(showMeanEMA ? basis : na, title = 'Keltner Channel EMA (Mean)', color = color.new(#BB6083, 40), linewidth = 1)
plot(showInnerBand ? upInnerKeltnerBand : na, title = 'Inner Upper Band', color = color.new(#DD8EAD, 30), linewidth = 1)
plot(showInnerBand ? downInnerKeltnerBand : na, title = 'Inner Lower Band', color = color.new(#DD8EAD, 30), linewidth = 1)
plot(showOuterBand ? upOuterKeltnerBand : na, title = 'Outer Upper Band', color = color.new(#DD8EAD, 40), linewidth = 1)
plot(showOuterBand ? downOuterKeltnerBand : na, title = 'Outer Lower Band', color = color.new(#DD8EAD, 40), linewidth = 1)

// Plot Mean Deviations
// Outside the Upper Band
firstFreeBarUp = if signalBand == 'Inner'
    high <= upInnerKeltnerBand and close <= upInnerKeltnerBand
else if signalBand == 'Outer'
    high <= upOuterKeltnerBand and close <= upOuterKeltnerBand
numFreeBarsUp = ta.barssince(firstFreeBarUp)

color colorBarUp = color.from_gradient(numFreeBarsUp, 0, maxMomentumBars, color.new(upMomentumColor, 80), upMomentumColor)
barcolor(showDeviations and numFreeBarsUp > 0 ? colorBarUp : na, title = 'Mean Deviations Up')

// Outside Lower Band
firstFreeBarDown = if signalBand == 'Inner'
    low >= downInnerKeltnerBand and close >= downInnerKeltnerBand
else if signalBand == 'Outer'
    low >= downOuterKeltnerBand and close >= downOuterKeltnerBand
numFreeBarsDown = ta.barssince(firstFreeBarDown)

color colorBarDown = color.from_gradient(numFreeBarsDown, 0, maxMomentumBars, color.new(downMomentumColor, 80), downMomentumColor)
barcolor(showDeviations and numFreeBarsDown > 0 ? colorBarDown : na, title = 'Mean Deviations Down')


// Plot band crosses
longCrossCandle = signalSource == 'Wick' ? high : close
shortCrossCandle = signalSource == 'Wick' ? low : close

longCross = if barstate.isconfirmed
    if signalBand == 'Inner'
        longCrossCandle >= upInnerKeltnerBand
    else if signalBand == 'Outer'
        longCrossCandle >= upOuterKeltnerBand
else
    false

shortCross = if barstate.isconfirmed
    if signalBand == 'Inner'
        shortCrossCandle <= downInnerKeltnerBand
    else if signalBand == 'Outer'
        shortCrossCandle <= downOuterKeltnerBand
else
    false

showlongCross = showOnlyFirstSignal ? longCross and not longCross[1] : longCross
showshortCross = showOnlyFirstSignal ? shortCross and not shortCross[1] : shortCross

upTriangleShape = shape.triangledown
downTriangleShape = shape.triangleup

plotshape(showBandCross ? showlongCross : false, title = 'Normal Band Cross Up', style = upTriangleShape, size = size.tiny, color = color.new(upMomentumColor, 50), location = location.abovebar)
plotshape(showBandCross ? showshortCross : false, title = 'Normal Band Cross Down', style = downTriangleShape, size = size.tiny, color = color.new(downMomentumColor, 50), location = location.belowbar)

// Plot Free Bars Breakouts
longFreeBar = if signalBand == 'Inner'
    low >= upInnerKeltnerBand
else if signalBand == 'Outer'
    low >= upOuterKeltnerBand
shortFreeBar = if signalBand == 'Inner'
    high <= downInnerKeltnerBand
else if signalBand == 'Outer'
    high <= downInnerKeltnerBand

plotchar(showFreeBars ? longFreeBar : false, title = 'Free Bar Up', char = '★', size = size.tiny, color = color.new(upMomentumColor, 50), location = location.abovebar) //, display=display.none)
plotchar(showFreeBars ? shortFreeBar : false, title = 'Free Bar Down', char = '★', size = size.tiny, color = color.new(downMomentumColor, 50), location = location.belowbar) //, display=display.none)
